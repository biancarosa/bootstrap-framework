buildscript {
	repositories {
		mavenLocal()
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {}
}

plugins {
	id "groovy"
	id "maven"
	id "maven-publish"
	id "com.gradle.plugin-publish" version "0.9.1"
	id "com.jfrog.bintray" version "1.2"
}

version "1.0.2"
group = "com.siprell.plugins"
sourceCompatibility = "1.7"
targetCompatibility = "1.7"

ext {
	isReleaseVersion = !version.endsWith("SNAPSHOT")
}

repositories {
	jcenter()
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	testCompile("org.spockframework:spock-core:1.0-groovy-2.3")
	testCompile("org.springframework.boot:spring-boot:1.2.1.RELEASE")
}

task sourcesJar(type: Jar) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task(console, dependsOn: "classes", type: JavaExec) {
	main = "groovy.ui.Console"
	classpath = sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		showStandardStreams = true
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			artifactId "bootstrap-framework"
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name "bootstrap-framework"
					description "Gradle plugin for integrating the Bootstrap Framework and Font Awesome"
					url "https://github.com/kensiprell/bootstrap-framework"
					scm {
						url "https://github.com/kensiprell/bootstrap-framework"
						connection "scm:https://github.com/kensiprell/bootstrap-framework.git"
						developerConnection "scm:git://github.com/kensiprell/bootstrap-framework.git"
					}
					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/license/LICENSE-2.0.txt"
							distribution "repo"
						}
					}
					developers {
						developer {
							id "kensiprell"
							name "Ken Siprell"
							email "ken.siprell@gmail.com"
						}
					}
				}
			}
			from components.java
			artifact sourcesJar
		}
	}
}

bintray {
	if (project.hasProperty("bintrayUser")) {
		user = bintrayUser
		key = bintrayKey
	}
	publications = ["maven"]
	pkg {
		repo = "gradle-plugins"
		name = "bootstrap-framework"
		desc = "Gradle plugin for integrating the Bootstrap Framework and Font Awesome"
		websiteUrl = "https://github.com/kensiprell/bootstrap-framework"
		issueTrackerUrl = "https://github.com/kensiprell/bootstrap-framework/issues"
		vcsUrl = "https://github.com/kensiprell/bootstrap-framework.git"
		licenses = ["Apache-2.0"]
		labels = ["bootstrap", "fontawesome", "font awesome", "gradle"]
		publicDownloadNumbers = true
	}
}

pluginBundle {
	website = "https://github.com/kensiprell/bootstrap-framework"
	vcsUrl = "https://github.com/kensiprell/bootstrap-framework.git"
	description = "Gradle plugin for integrating the Bootstrap Framework and Font Awesome"
	tags = ["bootstrap", "fontawesome", "font awesome", "gradle"]

	plugins {
		bootstrapFrameworkPlugin {
			id = "com.siprell.plugins.bootstrap-framework"
			displayName = "Bootstrap Framework Plugin for Gradle"
		}
	}
}

bintrayUpload.dependsOn build, sourcesJar
